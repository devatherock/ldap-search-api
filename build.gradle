plugins {
    id 'groovy'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'io.micronaut.application' version '3.7.10'
    id 'jacoco'
    id 'org.sonarqube' version '4.2.0.3129'
    id 'com.github.kt3k.coveralls' version '2.12.2'
    id 'com.diffplug.spotless' version '6.19.0'
    id 'org.owasp.dependencycheck' version '8.2.1'
}

version = '2.0.0'
group = 'io.github.devatherock'

repositories {
    mavenCentral()
}

micronaut {
    runtime('netty')
    processing {
        incremental(true)
        annotations('io.github.devatherock.ldapsearch.*')
    }
}

dependencies {
    def lombokVersion = '1.18.28'

    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor("io.micronaut.openapi:micronaut-openapi")

    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    compileOnly group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1'
    compileOnly("org.graalvm.nativeimage:svm")

    implementation("io.micronaut:micronaut-validation")
    implementation("io.micronaut:micronaut-runtime")
    implementation("io.swagger.core.v3:swagger-annotations")
    implementation("io.micronaut:micronaut-management")
    implementation("io.micronaut.micrometer:micronaut-micrometer-core")

    implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '7.3'
    implementation group: 'org.codehaus.janino', name: 'janino', version: '3.1.9'
    implementation group: 'javax.inject', name: 'javax.inject', version: '1'
    implementation "ch.qos.logback:logback-classic:1.4.7"

    testImplementation group: 'org.spockframework', name: 'spock-core', version: '2.3-groovy-3.0'
    testImplementation group: 'cglib', name: 'cglib-nodep', version: '3.3.0'
    testImplementation group: 'org.objenesis', name: 'objenesis', version: '3.3'
    testImplementation group: 'io.micronaut.test', name: 'micronaut-test-spock'
    testImplementation group: 'com.unboundid', name: 'unboundid-ldapsdk', version: '6.0.9'
    testImplementation group: 'io.micronaut', name: 'micronaut-http-client'
    testImplementation group: 'ch.qos.logback', name: 'logback-classic'
    testImplementation group: 'org.codehaus.groovy', name: 'groovy-json', version: '3.0.17'
}

application {
    mainClass.set("io.github.devatherock.ldapsearch.Application")
}

java {
    sourceCompatibility = JavaVersion.toVersion("1.8")
    targetCompatibility = JavaVersion.toVersion("1.8")
}

test {
    if (Boolean.getBoolean('graalvm')) {
        jvmArgs("-agentlib:native-image-agent=access-filter-file=$projectDir/src/main/resources/graal/access-filter.json,config-output-dir=$projectDir/build/graal")
    }
}

// Generates resource-config.json file for graalvm
build.dependsOn(generateResourcesConfigFile)

tasks.withType(Test) {
    useJUnitPlatform()
}

run {
    systemProperties(System.properties)
}

ext.jacoco = [
    exclusions: [
        'io/github/devatherock/ldapsearch/Application.class',
    ],
    coverageThresholds: [
        'io.github.devatherock.logback.LogbackConfigInitializer': [
            'BRANCH': 0.75,
            'COMPLEXITY': 0.62,
            'INSTRUCTION': 0.89,
            'LINE': 0.88
        ],
    ]
]

apply from: 'https://raw.githubusercontent.com/devatherock/gradle-includes/master/integration.gradle'
apply from: 'https://raw.githubusercontent.com/devatherock/gradle-includes/master/checks.gradle'